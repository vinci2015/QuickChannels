apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def projectName = "quick-channels"
def mavenDesc = 'a plugin which helps you generate greate amount of channel apks in few seconds.'
def baseUrl = 'https://github.com/vinci2015'
def siteUrl = "${baseUrl}/QuickChannels"
def gitUrl = "${baseUrl}/QuickChannels.git"
def issueUrl = "${baseUrl}/QuickChannels/issues"

def licenseIds = ['Apache-2.0']
def licenseNames = ['The Apache Software License, Version 2.0']
def licenseUrls = ['http://www.apache.org/licenses/LICENSE-2.0.txt']
def inception = '2017'

def username = 'vinci2015'

install {
    repositories {
        mavenInstaller {
            pom.project {
                // Description
                name projectName
                description mavenDesc
                url siteUrl

                // Archive
                groupId 'com.github.vinci.quick-channels'
                artifactId 'plugin'
                version '1.0.2'

                // License
                inceptionYear inception
                licenses {
                    licenseNames.eachWithIndex { ln, li ->
                        license {
                            name ln
                            url licenseUrls[li]
                        }
                    }
                }
                developers {
                    developer {
                        name username
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allGroovy
    classifier = 'sources'
}


task javadocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}


bintray {
    Properties properties = new Properties()
    properties.load(file('../local.properties').newDataInputStream())
    user = properties.getProperty('BINTRAY_USER')
    key = properties.getProperty('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = projectName
        desc = mavenDesc
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        labels = ['gradle', 'plugin', 'channels']
        licenses = licenseIds
        publish = true
        publicDownloadNumbers = true
    }
}